rule "Primo VE - contents 505gtr"
#version 2022 Q2
#replace the delimiter in subfield t with prefix & suffix with html italic
#replace the delimiter in subfield r with slash and html line break
#repeat for each new heading in subfield g
#replace any occurrence of double hyphen with blank
#create the contents display in the PNX from the temp field
#version 2022 Q3
#remove italic
#add semicolon for repeated subfields t
when
            MARC."505" has any "g,t,r"
then
            set TEMP"1" to MARC."505" sub without sort "g,t,r" wrap subfields
            replace wrapping delimiters (TEMP"1","g","","<br>")
            replace wrapping delimiters (TEMP"1","t",";££","££")
           replace wrapping delimiters (TEMP"1","r"," / ","<br>")
           replace string by string (TEMP"1",";££","")
	   replace string by string (TEMP"1","££"," ; ")
            replace string by string (TEMP"1","--","")
            remove string (TEMP"1","<<")
            remove string (TEMP"1",">>")
            remove substring using regex (TEMP"1","(/|:| ; |=|,)+$")
            create pnx."display"."contents" with TEMP"1"
end

rule "Primo VE - contents 505a"
#version 2022 Q2
#creates list style TOC
#if the record has 505 with subfield a
#copy the subfield a to temp field
#add a line break between each a
#create the contents display in the PNX from the temp field
when
            MARC is "505"."a"
then
            set TEMP"1" to MARC."505" sub without sort "a"
            replace string by string (TEMP"1","(--|;)","<br>")
            remove string (TEMP"1","<<")
            remove string (TEMP"1",">>")
            create pnx."display"."contents" with TEMP"1"
end

rule "Primo VE - contents 505uy"
#version 2022 Q2
#add rule for display url
when
            MARC "505" has any "u,y"
then
            set TEMP"1" to MARC."505" subfields "u"
            set TEMP"2" to MARC."505" subfields "y"
            add suffix (TEMP"2","<a/>")
            add prefix (TEMP"1","<a target=\"_blank\" href=\"")
            add suffix (TEMP"1","\">")
            concatenate with delimiter (TEMP"1",TEMP"2","")
            create pnx."display"."contents" with TEMP"1"
end

rule "Prima_Display - contents 880-505"
#version 2022 Q2
#repeat changes like in 505 gtr
	when
		MARC."880" has any "g,t,r" AND
		MARC."880"."6" match "505-.*"
	then		
                 set TEMP"1" to MARC."880" sub without sort "g,t,r" wrap subfields
             replace wrapping delimiters (TEMP"1","g","","<br>")
            replace wrapping delimiters (TEMP"1","t",";££","££")
           replace wrapping delimiters (TEMP"1","r"," / ","<br>")
           replace string by string (TEMP"1",";££","")
	   replace string by string (TEMP"1","££"," ; ")
            replace string by string (TEMP"1","--","")
            remove string (TEMP"1","<<")
            remove string (TEMP"1",">>")
            remove substring using regex (TEMP"1","(/|:| ; |=|,)+$")
            create pnx."display"."contents" with TEMP"1"
end

rule "Primo VE - contents 880-505a"
# version 2022 Q2
# if the record has 880 $6 505-* with subfield a
# field is displayed tel quel
# create the contents display in the PNX from the temp field
	when
		MARC is "880"."a" AND
		MARC."880"."6" match "505-.*"
	then
		set TEMP"1" to MARC."880" sub without sort "a"
		replace string by string (TEMP"1","(---)"," ")
		replace string by string (TEMP"1","(--|;)"," ; ")
		remove string (TEMP"1","<<")
		remove string (TEMP"1",">>")
		create pnx."display"."contents" with TEMP"1"
end
